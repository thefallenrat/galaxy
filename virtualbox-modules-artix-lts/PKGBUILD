# Maintainer: Rafli Akmal <rafliakmaltejakusuma AT gmail DOT com>
# Contributor: Chris Cromer <cromer@cromnix.org>
# Contributor: SÃ©bastien Luttringer
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgbase=virtualbox-modules-artix-lts
pkgname=('virtualbox-host-modules-artix-lts' 'virtualbox-guest-modules-artix-lts')
pkgver=5.2.4
pkgrel=7
_linux_major=4
_linux_minor=9
_linux_revision=76
arch=('x86_64')
url='http://virtualbox.org'
license=('GPL')
makedepends=("linux-lts>=${_linux_major}.${_linux_minor}.${_linux_revision}"
             "linux-lts<${_linux_major}.$((_linux_minor+1))"
             "linux-lts-headers>=${_linux_major}.${_linux_minor}.${_linux_revision}"
             "linux-lts-headers<${_linux_major}.$((_linux_minor+1))"
             "virtualbox-host-dkms>=$pkgver"
             "virtualbox-guest-dkms>=$pkgver"
             "libelf")

_extramodules=extramodules-${_linux_major}.${_linux_minor}-lts

package_virtualbox-host-modules-artix-lts(){
  _kernver="$(cat /usr/lib/modules/$_extramodules/version)"
  pkgdesc='Virtualbox host kernel modules for Artix LTS Kernel'
  depends=("linux-lts>=${_linux_major}.${_linux_minor}.${_linux_revision}"
           "linux-lts<${_linux_major}.$((_linux_minor+1))")
  replaces=('virtualbox-modules' 'virtualbox-host-modules')
  conflicts=('virtualbox-modules' 'virtualbox-host-modules'
             'virtualbox-host-dkms')
  provides=('VIRTUALBOX-HOST-MODULES')

  cd "/var/lib/dkms/vboxhost/${pkgver}_OSE/$_kernver/$CARCH/module"
  install -Dt "$pkgdir/usr/lib/modules/$_extramodules" -m644 *

  # compress each module individually
  find "$pkgdir" -name '*.ko' -exec gzip -n {} +

  # systemd module loading
  printf "vboxdrv\nvboxpci\nvboxnetadp\nvboxnetflt\n" |
  install -Dm644 /dev/stdin "$pkgdir/usr/lib/modules-load.d/$pkgname.conf"
}

package_virtualbox-guest-modules-artix-lts(){
  _kernver="$(cat /usr/lib/modules/$_extramodules/version)"
  pkgdesc='Virtualbox guest kernel modules for Artix LTS Kernel'
  license=('GPL')
  depends=("linux-lts>=${_linux_major}.${_linux_minor}.${_linux_revision}"
           "linux-lts<${_linux_major}.$((_linux_minor+1))")
  replaces=('virtualbox-archlinux-modules' 'virtualbox-guest-modules')
  conflicts=('virtualbox-archlinux-modules' 'virtualbox-guest-modules'
             'virtualbox-guest-dkms')
  provides=('VIRTUALBOX-GUEST-MODULES')

  cd "/var/lib/dkms/vboxguest/${pkgver}_OSE/$_kernver/$CARCH/module"
  install -Dt "$pkgdir/usr/lib/modules/$_extramodules" -m644 *

  # compress each module individually
  find "$pkgdir" -name '*.ko' -exec gzip -n {} +

  # systemd module loading
  printf "vboxguest\nvboxsf\nvboxvideo\n" |
  install -Dm644 /dev/stdin "$pkgdir/usr/lib/modules-load.d/$pkgname.conf"
}
