# Maintainer: Rafli Akmal <thefallenrat@artixlinux.org>
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: xduugu
# Contributor: nh2
# Contributor: Steven Davidovitz <steviedizzle ð gmail đ com>
# Contributor: Nick B <Shirakawasuna ð gmail đ com>
# Contributor: Christof Musik <christof ð senfdax đ de>
# Contributor: Stefan Rupp <archlinux ð stefanrupp đ de>
# Contributor: Ignas Anikevicius <anikevicius ð gmail đ com>

pkgname=tp_smapi-lts
_pkgname=tp-smapi
pkgver=0.42
pkgrel=25.5
_linux_major=4
_linux_minor=9
_linux_revision=76
_linux_rel=1
pkgdesc="Modules for ThinkPad's SMAPI functionality"
arch=('x86_64')
url='https://github.com/evgeni/tp_smapi'
license=('GPL')
depends=("linux-lts>=${_linux_major}.${_linux_minor}.${_linux_revision}"
         "linux-lts<${_linux_major}.$((_linux_minor+1))")
makedepends=("linux-lts-headers>=${_linux_major}.${_linux_minor}.${_linux_revision}"
             "linux-lts-headers<${_linux_major}.$((_linux_minor+1))")
install="$pkgname.install"
source=("$_pkgname-$pkgver.tar.gz::https://github.com/evgeni/${pkgname/-lts/}/archive/$_pkgname/$pkgver.tar.gz")
md5sums=('6a51d3aa459ad7a6ebfbb8c29527b3ee')

_extradir="/usr/lib/modules/extramodules-${_linux_major}.${_linux_minor}-lts"
_kernver="${_linux_major}.${_linux_minor}.${_linux_revision}-${_linux_rel}-lts"

build() {
  cd ${pkgname/-lts/}-$_pkgname-$pkgver

  # https://bugs.archlinux.org/task/54975 (kernel has no _GLOBAL_OFFSET_TABLE_):
  # Clear EXTRA_CFLAGS since it defaults to injecting CFLAGS and -fno-plt breaks the modules

  make HDAPS=1 KVER="$_kernver" EXTRA_CFLAGS=
}

package() {
  cd ${pkgname/-lts/}-$_pkgname-$pkgver

  # install kernel modules
  find . -name "*.ko" -exec install -Dt "$pkgdir$_extradir" {} +

  # compress kernel modules
  find "$pkgdir" -name "*.ko" -exec gzip -n -9 {} +

  # load module on startup
  echo tp_smapi | install -Dm644 /dev/stdin "$pkgdir/usr/lib/modules-load.d/$pkgname.conf"

  # update kernel version in install file
  sed -ri "s|^(extramodules=).*\$|\1$_extradir|" "$startdir/$pkgname.install"
}
