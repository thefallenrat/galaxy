# Maintainer: Rafli Akmal <thefallenrat@artixlinux.org>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Bob Fanger < bfanger(at)gmail >
# Contributor: Filip <fila pruda com>, Det < nimetonmaili(at)gmail >

pkgname=r8168
pkgver=8.045.08
pkgrel=25
_linux_major=4
_linux_minor=15
#_linux_revision=15
_linux_rel=1
pkgdesc="A kernel module for Realtek 8168 network cards"
url="http://www.realtek.com.tw"
license=("GPL")
arch=('x86_64')
depends=('glibc'
         "linux>=${_linux_major}.${_linux_minor}"
         "linux<${_linux_major}.$((_linux_minor+1))")
makedepends=("linux-headers>=${_linux_major}.${_linux_minor}"
             "linux-headers<${_linux_major}.$((_linux_minor+1))")
source=(https://github.com/mtorromeo/r8168/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('18161cb72fc872a9aed194514f7b8fb8b255b6fa6ed3d2dd459700eaad575f31')

_extramodules=extramodules-${_linux_major}.${_linux_minor}-ARTIX
#_kernver="${_linux_major}.${_linux_minor}.${_linux_revision}-${_linux_rel}-ARTIX"
_kernver="${_linux_major}.${_linux_minor}-${_linux_rel}-ARTIX"

build() {
	msg2 "Kernel = $_kernver"

	cd "$pkgname-$pkgver"
	# avoid using the Makefile directly -- it doesn't understand
	# any kernel but the current.
	make -C /usr/lib/modules/$_kernver/build \
			SUBDIRS="$srcdir/$pkgname-$pkgver/src" \
			EXTRA_CFLAGS="-DCONFIG_R8168_NAPI -DCONFIG_R8168_VLAN" \
			modules
}

package() {
	cd "$pkgname-$pkgver"
	install -Dt "$pkgdir/usr/lib/modules/$_extramodules" -m644 src/r8168.ko
	find "$pkgdir" -name '*.ko' -exec gzip -n {} +

	echo "blacklist r8169" | \
		install -Dm644 /dev/stdin "$pkgdir/usr/lib/modprobe.d/r8168.conf"
}
