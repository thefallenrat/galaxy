# Maintainer: Rafli Akmal <thefallenrat@artixlinux.org>
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: xduugu
# Contributor: nh2
# Contributor: Steven Davidovitz <steviedizzle ð gmail đ com>
# Contributor: Nick B <Shirakawasuna ð gmail đ com>
# Contributor: Christof Musik <christof ð senfdax đ de>
# Contributor: Stefan Rupp <archlinux ð stefanrupp đ de>
# Contributor: Ignas Anikevicius <anikevicius ð gmail đ com>

pkgname=tp_smapi
_pkgname=tp-smapi
pkgver=0.42+7+g76c5120
pkgrel=1
_linux_major=4
_linux_minor=15
_linux_revision=0
_linux_rel=1
pkgdesc="Modules for ThinkPad's SMAPI functionality"
arch=('x86_64')
url='https://github.com/evgeni/tp_smapi'
license=('GPL')
depends=("linux>=${_linux_major}.${_linux_minor}"
         "linux<${_linux_major}.$((_linux_minor+1))")
makedepends=("linux-headers>=${_linux_major}.${_linux_minor}"
             "linux-headers<${_linux_major}.$((_linux_minor+1))"
             "git")
_commit=76c5120f7be4880cf2c6801f872327e4e70c449f  # master
source=("git+https://github.com/evgeni/tp_smapi#commit=$_commit")
sha256sums=('SKIP')

_extradir="/usr/lib/modules/extramodules-${_linux_major}.${_linux_minor}-ARTIX"
_kernver="${_linux_major}.${_linux_minor}.${_linux_revision}-${_linux_rel}-ARTIX"

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^tp-smapi\///;s/-/+/g'
}

build() {
  cd $pkgname

  # https://bugs.archlinux.org/task/54975 (kernel has no _GLOBAL_OFFSET_TABLE_):
  # Clear EXTRA_CFLAGS since it defaults to injecting CFLAGS and -fno-plt breaks the modules

  make HDAPS=1 KVER="$_kernver" EXTRA_CFLAGS=
}

package() {
  cd $pkgname

  # install kernel modules
  find . -name "*.ko" -exec install -Dt "$pkgdir$_extradir" {} +

  # compress kernel modules
  find "$pkgdir" -name "*.ko" -exec xz {} +

  # load module on startup
  echo tp_smapi | install -Dm644 /dev/stdin "$pkgdir/usr/lib/modules-load.d/$pkgname.conf"
}
