# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=libmongoc
pkgver=1.9.2
pkgrel=1
pkgdesc='A client library written in C for MongoDB'
arch=('x86_64')
url='http://www.mongodb.org/display/DOCS/C+Language+Center'
license=('Apache')
depends=('libbson' 'libsasl' 'snappy' 'zlib')
checkdepends=('python-pifpaf' 'mongodb')
source=("$pkgname-$pkgver.tar.gz::https://github.com/mongodb/mongo-c-driver/archive/$pkgver.tar.gz")
sha512sums=('a2c819da77aef93ce261093e98e8e8c41c449af56bd03d875e2838a067ae71b5ceb16fed2fb8df9458c84310451b813464377592806fc9ac39d9df2f4ddba83b')

prepare() {
  cd mongo-c-driver-$pkgver
  # Remove check for bundled libbson
  sed -i 's|src/libbson/autogen.sh|/usr/bin/true|' autogen.sh
}

build() {
  cd mongo-c-driver-$pkgver
  PTHREAD_LIBS=-pthread ./autogen.sh --prefix=/usr
  make
}

check() {
  cd mongo-c-driver-$pkgver
  pifpaf run mongodb --port 27017 make test || warning "Tests failed"
}

package() {
  cd mongo-c-driver-$pkgver
  make DESTDIR="$pkgdir/" install
}
