# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=libmongoc
pkgver=1.9.1
pkgrel=1
pkgdesc='A client library written in C for MongoDB'
arch=('x86_64')
url='http://www.mongodb.org/display/DOCS/C+Language+Center'
license=('Apache')
depends=('libbson' 'libsasl' 'snappy' 'zlib')
checkdepends=('python-pifpaf' 'mongodb')
source=("$pkgname-$pkgver.tar.gz::https://github.com/mongodb/mongo-c-driver/archive/$pkgver.tar.gz")
sha512sums=('8918b33bcb346a49b84b9e99f5f1ebf0b5895eef7a72dc3e7c3a47737e42e3fa57d820604ec51a47dcfc14be962ad32bbc621197ff639cdff917a56e6ec6e708')

prepare() {
  cd mongo-c-driver-$pkgver
  # Remove check for bundled libbson
  sed -i 's|src/libbson/autogen.sh|/usr/bin/true|' autogen.sh
}

build() {
  cd mongo-c-driver-$pkgver
  PTHREAD_LIBS=-pthread ./autogen.sh --prefix=/usr
  make
}

check() {
  cd mongo-c-driver-$pkgver
  pifpaf run mongodb --port 27017 make test || warning "Tests failed"
}

package() {
  cd mongo-c-driver-$pkgver
  make DESTDIR="$pkgdir/" install
}
