# Maintainer: Rafli Akmal <thefallenrat@artixlinux.org>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=r8168-lts
_pkgname=r8168
pkgver=8.045.08
pkgrel=7.5
_linux_major=4
_linux_minor=9
_linux_revision=76
_linux_rel=1
pkgdesc="A kernel module for Realtek 8168 network cards for linux-lts"
url="http://www.realtek.com.tw"
license=("GPL")
arch=('x86_64')
depends=('glibc'
         "linux-lts>=${_linux_major}.${_linux_minor}.${_linux_revision}"
         "linux-lts<${_linux_major}.$((_linux_minor+1))")
makedepends=("linux-lts-headers>=${_linux_major}.${_linux_minor}.${_linux_revision}"
             "linux-lts-headers<${_linux_major}.$((_linux_minor+1))")
install=$pkgname.install
source=(https://github.com/mtorromeo/r8168/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('18161cb72fc872a9aed194514f7b8fb8b255b6fa6ed3d2dd459700eaad575f31')

_extramodules=extramodules-${_linux_major}.${_linux_minor}-lts
_kernver="${_linux_major}.${_linux_minor}.${_linux_revision}-${_linux_rel}-lts"

build() {
	cd "$_pkgname-$pkgver"
	make -C /usr/lib/modules/$_kernver/build \
			SUBDIRS="$srcdir/$_pkgname-$pkgver/src" \
			EXTRA_CFLAGS="-DCONFIG_R8168_NAPI -DCONFIG_R8168_VLAN" \
			modules
}

package() {
	cd "$_pkgname-$pkgver"
	install -Dm644 src/$_pkgname.ko "$pkgdir/usr/lib/modules/$_extramodules/$_pkgname.ko"
	find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
}
