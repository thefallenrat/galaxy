# Maintainer: Rafli Akmal <rafliakmaltejakusuma AT gmail DOT com>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: M0Rf30
# Contributor: Samsagax <samsagax@gmail.com>

pkgname=bbswitch-lts
_basename=bbswitch
pkgver=0.8
pkgrel=57
_linux_major=4
_linux_minor=14
_linux_revision=17
_extramodules=extramodules-${_linux_major}.${_linux_minor}-lts # Don't forget to update bbswitch-lts.install
pkgdesc="Kernel module allowing to switch dedicated graphics card on Optimus laptops"
arch=('x86_64')
url="http://github.com/Bumblebee-Project/bbswitch"
license=('GPL')
depends=("linux-lts>=${_linux_major}.${_linux_minor}.${_linux_revision}"
         "linux-lts<${_linux_major}.$((_linux_minor+1))")
makedepends=("linux-lts-headers>=${_linux_major}.${_linux_minor}.${_linux_revision}"
             "linux-lts-headers<${_linux_major}.$((_linux_minor+1))"
             "libelf")
install=${pkgname}.install
source=("${_basename}-$pkgver.tar.gz::https://github.com/Bumblebee-Project/bbswitch/archive/v${pkgver}.tar.gz")
md5sums=('5b116b31ace3604ddf9d1fc1f4bc5807')

build() {
  cd ${srcdir}/${_basename}-${pkgver}

  _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"

  make KDIR=/lib/modules/${_kernver}/build
}

package() {
  cd ${srcdir}/${_basename}-${pkgver}

  install -Dm644 bbswitch.ko "${pkgdir}"/usr/lib/modules/${_extramodules}/bbswitch.ko
  gzip "${pkgdir}/usr/lib/modules/${_extramodules}/bbswitch.ko"
}
