# Maintainer: Rafli Akmal <thefallenrat@artixlinux.org>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: M0Rf30
# Contributor: Samsagax <samsagax@gmail.com>

pkgbase=bbswitch
pkgname=(bbswitch bbswitch-dkms)
pkgver=0.8
pkgrel=102
_linux_major=4
_linux_minor=14
_linux_revision=15
_linux_rel=1
pkgdesc="Kernel module allowing to switch dedicated graphics card on Optimus laptops"
arch=('i686' 'x86_64')
url="http://github.com/Bumblebee-Project/bbswitch"
license=('GPL')
depends=("linux>=${_linux_major}.${_linux_minor}.${_linux_revision}"
         "linux<${_linux_major}.$((_linux_minor+1))")
makedepends=("linux-headers>=${_linux_major}.${_linux_minor}.${_linux_revision}"
             "linux-headers<${_linux_major}.$((_linux_minor+1))"
             "libelf")
source=("${pkgbase}-${pkgver}.tar.gz::https://github.com/Bumblebee-Project/bbswitch/archive/v${pkgver}.tar.gz")
md5sums=('5b116b31ace3604ddf9d1fc1f4bc5807')

_extramodules=extramodules-${_linux_major}.${_linux_minor}-ARTIX
_kernver="${_linux_major}.${_linux_minor}.${_linux_revision}-${_linux_rel}-ARTIX"

build() {
  cd ${pkgbase}-${pkgver}
  make KDIR=/lib/modules/${_kernver}/build
}

package_bbswitch() {
  cd ${pkgbase}-${pkgver}
  install -Dt "${pkgdir}/usr/lib/modules/${_extramodules}" -m644 bbswitch.ko
  find "${pkgdir}" -name '*.ko' -exec gzip -n {} +
}

package_bbswitch-dkms() {
  depends=('dkms')
  conflicts=('bbswitch')
  provides=('bbswitch')

  cd ${pkgbase}-${pkgver}
  install -Dt "${pkgdir}/usr/src/${pkgbase}-${pkgver}" -m644 Makefile bbswitch.c dkms/dkms.conf
}
