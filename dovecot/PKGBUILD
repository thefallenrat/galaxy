# Maintainer: Rafli Akmal <thefallenrat@artixlinux.org>
# Contributor: Johannes Löthberg <johannes@kyriasis.com>
# Contributor:  Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Paul Mattal <paul@mattal.com>
# Contributor: Federico Quagliata (quaqo) <quaqo@despammed.com>
# Contributor: GARETTE Emmanuel <gnunux at laposte dot net>

# --->>> remember to rebuild/bump pigeonhole in one step <<<---

pkgname=dovecot
pkgver=2.3.0
pkgrel=2

pkgdesc="An IMAP and POP3 server written with security primarily in mind"
url="http://dovecot.org/"
arch=('x86_64')
license=("LGPL")

depends=('krb5' 'openssl' 'sqlite' 'libmariadbclient'
         'postgresql-libs' 'bzip2' 'lz4' 'expat' 'curl' 'pam')
makedepends=('libcap' 'libldap' 'clucene')
optdepends=('libldap: ldap plugin'
            'xz: imap zlib  plugin'
            'clucene: alternative FTS indexer')

provides=('imap-server' 'pop3-server')

source=("https://dovecot.org/releases/2.3/dovecot-$pkgver.tar.gz"{,.sig}
        dovecot.sysusersd
        dovecot.tmpfilesd)

sha256sums=('de60cb470d025e4dd0f8e8fbbb4b9316dfd4930eb949d307330669ffbeaf8581'
            'SKIP'
            'c5e3a8ffe23e5deb4f7893d9877d972347c2ee45c4ebf713de85c537e47cfcaf'
            'bb7620be7fc0217d77fcac940441c2abbd9efc6ef660153288d09ba412e27e06')
validpgpkeys=('E643F0BDFDCD04D9FFCB6279C948525140558AC9' # Timo Sirainen <tss@iki.fi>
              '2BE74AAB3EE754DFB9C80D3318A348AEED409DA1') # Dovecot Community Edition

prepare() {
  cd dovecot-ce-$pkgver
  # fix path in helper script
  sed -i 's:OPENSSLCONFIG=${OPENSSLCONFIG-dovecot-openssl.cnf}:OPENSSLCONFIG=${OPENSSLCONFIG- /etc/ssl/dovecot-openssl.cnf}:' doc/mkcert.sh
}

build() {
  cd dovecot-ce-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --localstatedir=/var \
    --libexecdir=/usr/lib \
    --with-moduledir=/usr/lib/dovecot/modules \
    --with-systemdsystemunitdir=no \
    --disable-static \
    --with-nss \
    --with-pam \
    --with-mysql \
    --with-pgsql \
    --with-sqlite \
    --with-ssl=openssl \
    --with-ssldir=/etc/ssl \
    --with-gssapi \
    --with-ldap=plugin \
    --with-zlib \
    --with-bzlib \
    --with-lzma \
    --with-lz4 \
    --with-libcap \
    --with-solr \
    --with-lucene \
    --with-docs

  make
}

check() {
  cd dovecot-ce-$pkgver
  make check
}

package() {
  # system user/group dovenull - 74
  # system user/group dovecot  - 76

  cd dovecot-ce-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/dovecot.sysusersd" \
    "$pkgdir/usr/lib/sysusers.d/dovecot.conf"
  install -Dm644 "$srcdir/dovecot.tmpfilesd" \
    "$pkgdir/usr/lib/tmpfiles.d/dovecot.conf"
  install -d -m755 "$pkgdir/etc/dovecot/conf.d"
  rm -f $pkgdir/etc/dovecot/README

  # install mkcert helper script
  install -m 755  doc/mkcert.sh "$pkgdir/usr/lib/dovecot/mkcert.sh"
}
