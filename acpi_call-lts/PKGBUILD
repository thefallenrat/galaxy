# Maintainer: Rafli Akmal <thefallenrat@artixlinux.org>
# Contributor: Maxime Gauduin <alucryd@gmail.com>
# Contributor: mortzu <me@mortzu.de>
# Contributor: fnord0 <fnord0@riseup.net>

pkgname=acpi_call-lts
pkgver=1.1.0
pkgrel=55
_linux_major=4
_linux_minor=9
_linux_revision=77
_linux_rel=1
pkgdesc='A linux kernel module that enables calls to ACPI methods through /proc/acpi/call'
arch=('x86_64')
url='https://github.com/mkottman/acpi_call'
license=('GPL')
depends=("linux-lts>=${_linux_major}.${_linux_minor}.${_linux_revision}"
         "linux-lts<${_linux_major}.$((_linux_minor+1))")
makedepends=("linux-lts-headers>=${_linux_major}.${_linux_minor}.${_linux_revision}"
             "linux-lts-headers<${_linux_major}.$((_linux_minor+1))")
provides=('acpi_call')
install='acpi_call.install'
source=("acpi_call-${pkgver}.tar.gz::https://github.com/mkottman/acpi_call/archive/v${pkgver}.tar.gz")
sha256sums=('d0d14b42944282724fca76f57d598eed794ef97448f387d1c489d85ad813f2f0')

_extramodules=extramodules-${_linux_major}.${_linux_minor}-lts
_kernver="${_linux_major}.${_linux_minor}.${_linux_revision}-${_linux_rel}-lts"

prepare() {
  cd acpi_call-${pkgver}

  # Fix build with Linux >= 3.17
  sed -i 's|acpi/acpi.h|linux/acpi.h|' acpi_call.c
}

build() {
  cd ${pkgname%-*}-${pkgver}

  make KVERSION="${_kernver}"
}

package() {
  cd ${pkgname%-*}-${pkgver}

  install -dm 755 "${pkgdir}"/usr/lib/{modules/${_extramodules},modules-load.d}
  install -m 644 ${pkgname%-*}.ko "${pkgdir}"/usr/lib/modules/${_extramodules}/
  gzip "${pkgdir}"/usr/lib/modules/${_extramodules}/${pkgname%-*}.ko
  echo ${pkgname%-*} > "${pkgdir}"/usr/lib/modules-load.d/${pkgname}.conf

  install -dm 755 "${pkgdir}"/usr/share/${pkgname}
  cp -dr --no-preserve='ownership' {examples,support} "${pkgdir}"/usr/share/${pkgname}/
}

# vim: ts=2 sw=2 et:
